buildscript {
  ext {
    springBootVersion = '2.1.3.RELEASE'
    spotlessVersion = '3.17.0'
    asciidoctorVersion = '1.5.10'
    repositorioLocal = {
      jcenter()
    }
  }
  repositories repositorioLocal
  dependencies {
    classpath "com.diffplug.spotless:spotless-plugin-gradle:${spotlessVersion}"
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath "org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorVersion}"
  }
}

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'codenarc'
apply plugin: 'com.diffplug.gradle.spotless'
apply plugin: 'org.asciidoctor.convert'

group = 'com.brunorozendo'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories repositorioLocal

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'

  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-web'


  implementation("org.codehaus.groovy:groovy:2.5.6")


  /** tests*/
  testImplementation('org.codehaus.groovy:groovy-sql:2.5.6')
  testImplementation('org.codehaus.groovy:groovy-templates:2.5.6')
  testImplementation('org.codehaus.groovy:groovy-test:2.5.6')
  testImplementation('org.codehaus.groovy:groovy-nio:2.5.6')
  testImplementation('org.codehaus.groovy:groovy-macro:2.5.6')
  testImplementation('org.codehaus.groovy:groovy-xml:2.5.6')
  testImplementation('org.codehaus.groovy:groovy-json:2.5.6')

  testImplementation('org.spockframework:spock-core:1.3-RC1-groovy-2.5')
  testImplementation('org.spockframework:spock-spring:1.3-RC1-groovy-2.5')
  testImplementation('com.athaydes:spock-reports:1.6.1')
  
  /**  fim teste */
}

codenarc {
  toolVersion '1.3'
}

codenarcTest {
  exclude '**/*.groovy'
}

spotless {
  enforceCheck true
  groovy {
    paddedCell()
    excludeJava()
    greclipse().configFile('config/spotless/greclipse.properties')
  }
  groovyGradle {
    paddedCell()
    greclipse().configFile('config/spotless/greclipse.properties')
  }
}


asciidoctor {
  sourceDir = file('build/docs/src')
  outputDir = file('build/docs')
}


tasks.register("docs") {
  dependsOn clean
  dependsOn test
  finalizedBy asciidoctor
}

